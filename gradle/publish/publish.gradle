apply plugin: 'maven'
apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'org.jetbrains.dokka'

// Bintray upload
group = groupName
version = versionName

def SITE_URL = 'https://github.com/chibatching/kotpref'
def GIT_URL = 'https://github.com/chibatching/kotpref.git'

def USER = System.getenv("bintray_user")
def KEY = System.getenv("bintray_api_key")

bintray {
    user = USER
    key = KEY

    configurations = ['archives'] //When uploading configuration files
    pkg {
        repo = 'maven'
        name = 'kotpref'
        desc = 'Android SharedPreferences delegation for Kotlin.'
        websiteUrl = SITE_URL
        issueTrackerUrl = 'https://github.com/chibatching/kotpref/issues'
        vcsUrl = GIT_URL
        licenses = ['Apache-2.0']
        labels = ['aar', 'android', 'kotlin']
        publicDownloadNumbers = true
        githubRepo = 'chibatching/Kotpref'
    }
}

tasks.named("dokkaHtml") {
    dokkaSourceSets {
        main {
            sourceLink {
                localDirectory.set(file("src/main/kotlin"))
                remoteUrl.set(uri("https://github.com/chibatching/Kotpref/tree/v${version}/${project.name}/src/main/kotlin").toURL())
                remoteLineSuffix.set("#L")
            }
        }
    }
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    archiveClassifier.set('sources')
}

task javadocJar(type: Jar, dependsOn: dokkaJavadoc) {
    archiveClassifier.set('javadoc')
    from dokkaJavadoc.outputDirectory
}

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                from components.release

                artifact javadocJar
                artifact sourcesJar

                pom {
                    artifactId archivesBaseName
                    name = moduleName

                    packaging 'aar'
                    url = SITE_URL

                    licenses {
                        license {
                            name = 'The Apache Software License, Version 2.0'
                            url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        }
                    }
                    developers {
                        developer {
                            id = 'chibatching'
                            name = 'Takao Chiba'
                            email = 'takao.chiba@gmail.com'
                        }
                    }
                    scm {
                        connection = GIT_URL
                        developerConnection = GIT_URL
                        url = SITE_URL
                    }
                }
            }
        }
    }
}
