import dependencies.Deps
import dependencies.Versions

apply plugin: 'org.jetbrains.dokka'

buildscript {

    ext.versionName = Versions.versionName
    ext.groupName = 'com.chibatching.kotpref'

    repositories {
        jcenter()
        google()
    }
    dependencies {
        classpath Deps.androidGradlePlugin
        classpath Deps.Kotlin.gradlePlugin
        classpath Deps.bintrayGradlePlugin
        classpath Deps.androidMavenGradlePlugin
        classpath Deps.dokkaGradlePlugin
    }
}

allprojects {
    repositories {
        jcenter()
        google()
    }
}

task replaceVersionInReadme {
    doLast {
        ant.replaceregexp(match: 'com\\.chibatching\\.kotpref:(.+):([0-9\\.]+)', replace: "com.chibatching.kotpref:\\1:${Versions.versionName}", flags: 'g') {
            fileset(dir: project.projectDir, includes: 'README.md')
        }
        ant.replaceregexp(match: 'androidx\\.lifecycle:lifecycle-livedata:([0-9\\.]+)', replace: "androidx.lifecycle:lifecycle-livedata:${Versions.liveData}", flags: 'g') {
            fileset(dir: project.projectDir, includes: 'README.md')
        }
        ant.replaceregexp(match: 'com\\.google\\.code\\.gson:gson:([0-9\\.]+)', replace: "com.google.code.gson:gson:${Versions.gson}", flags: 'g') {
            fileset(dir: project.projectDir, includes: 'README.md')
        }
        ant.replaceregexp(match: 'kotlin-([0-9\\.]+)-blue\\.svg', replace: "kotlin-${Versions.kotlin}-blue.svg", flags: 'g') {
            fileset(dir: project.projectDir, includes: 'README.md')
        }
    }
}

dokka {
    outputFormat = 'html'
    outputDirectory = 'docs/api'
    subProjects = ["kotpref", "enum-support", "gson-support", "initializer", "livedata-support", "preference-screen-dsl"]

    configuration {
        moduleName = project.name
        sourceLink {
            path = "./"
            url = "https://github.com/chibatching/Kotpref/tree/v${Versions.versionName}"
            lineSuffix = "#L"
        }
    }
}

task cleanApiDocs(type: Delete) {
    delete 'docs/api'
}

dokka.dependsOn cleanApiDocs
